System party 
mqttBroker "localhost" : 1883 

/* --------------------------- */
// DISPATCH
/* --------------------------- */

//REQUISITO: command sent by the smart-phone of Maitre: add food command, by specifying a food-code
Dispatch addFood : addFood(FOODCODE) //da master
Dispatch foodAdded : foodAdded(N) //a master
//REQUISITO: send a warning to master when code food doesn't exist
Dispatch warning : warning(FOODCODE) //a master

Dispatch answerFromFridgeCoap : answerFromFridgeCoap(ANSWER) //da fridge
Dispatch moveSpecificFoodOnRobot : moveSpecificFoodOnRobot(FCL) //a fridge

//A navi
Dispatch goHome : goHome(MIT)
Dispatch reachAppliance : reachAppliance(MIT, NAME)
Dispatch reachTable : reachTable(MIT)

//Da navi
Dispatch fridgeReached : fridgeReached(N) 
Dispatch homeReached : homeReached(N) 
Dispatch tableReached : tableReached(N) 

//A kb
Dispatch moveFoodToDomesticResource : moveFoodToDomesticResource(DR)

/* --------------------------- */
// CONTESTI
/* --------------------------- */ 

Context ctxDummyNavigator ip [host="localhost" port=8038] +mqtt
Context ctxParty ip [host="localhost" port=9005] +mqtt
Context ctxDummyForSprint5 ip [host="localhost" port=9001] +mqtt
Context ctxDummyFridge ip [host="localhost" port=9004] +mqtt
Context ctxDummyKb ip [host="localhost" port=9000] +mqtt

/* --------------------------- */
// ATTORI
/* --------------------------- */

ExternalQActor fridge context ctxDummyFridge
ExternalQActor master5 context ctxDummyForSprint5
ExternalQActor navi context ctxDummyNavigator
ExternalQActor kb context ctxDummyKb

QActor addfood context ctxParty {
	[" var answer=\"\" 
	var FoodCode=\"\" "]

	State s0 initial{
		println("ADDFOOD AVVIATO")
		run itunibo.coap.fridge.createClient("localhost", 5684, "serverfridge") //CoAP access
	}
	Goto waitCmd
	
	State waitCmd {
		println("")
		println("State waitCmd")
		println("in attesa di un comando")
    }
	Transition t0 whenMsg addFood -> requestForCode //da master

	State requestForCode {
		println("")
		println("State requestForCode")
		onMsg(addFood : addFood(FOODCODE)) {
			["FoodCode = payloadArg(0).toString()"]
			run itunibo.coap.fridge.put(FoodCode) //richiesta via coap
		}
	}
	Transition t1 whenMsg answerFromFridgeCoap -> checkAnswer //da fridge
	
	State checkAnswer {
		println("")
		println("State checkAnswer")
		onMsg(answerFromFridgeCoap : answerFromFridgeCoap(ANSWER)) {
			["answer = payloadArg(0).toString()"]
		}	
	}
	//se è yes procedo, altrimenti non faccio nulla e invio un warning
	Goto reachFridge if "(answer==\"yes\") " else handleWarning
	
	State reachFridge {
		println("")
		println("State reachFridge")
		println("Obiettivo FRIDGE")	
		forward navi -m reachAppliance : reachAppliance(addfood, fridge)
	}
	Transition t4 whenMsg fridgeReached -> inFridge
	
	State inFridge {
		println("")
		println("State inFridge")
		//FoodCode non è una lista, ma un solo elemento
		forward fridge -m moveSpecificFoodOnRobot : moveSpecificFoodOnRobot($FoodCode)
		delay 2000 
		println("Obiettivo TABLE")
		forward navi -m reachTable : reachTable(addfood)
	}
	Transition t4 whenMsg tableReached -> inTable
	
	State inTable{
		println("")
		println("State inTable")
		forward kb -m moveFoodToDomesticResource : moveFoodToDomesticResource(table)
		delay 2000
		println("Obiettivo RH")
		forward navi -m goHome : goHome(addfood)
	}
	Transition t3 whenMsg homeReached -> endAddFood
	
	State endAddFood {
		println("")
		println("State endAddFood")
		println("ADDFOOD terminata")
		delay 500
		forward master5 -m foodAdded : foodAdded
	}
	Goto waitCmd
	
	State handleWarning {
		//invia warning al maitre
		delay 500
		forward master5 -m warning : warning($FoodCode)
	}
	Goto waitCmd
	
}   
