System sprint5  
mqttBroker "localhost" : 1883 
//per COAP cambiare indirizzo in resourceObserverCoapClient.kt 

/* --------------------------- */
// DISPATCH
/* --------------------------- */

//messaggio scambiato con roomperimeterexplorer per iniziare a scansionare il perimetro della stanza
Dispatch scanningPerimeter : scanningPerimeter(N)
//messaggio scambiato con roomperimeterexplorer terminata la scansione del perimetro della stanza
Dispatch endPerimeter : endPerimeter(N)
//messaggio scambiato con findtable per indicare di iniziare a cercare il tavolo
Dispatch scanningTable : scanningTable(N)
Dispatch homeReached : homeReached(N)

//REQUISITO: command sent by the smart-phone of Maitre: prepare
Dispatch prepare : prepare(N) 
Dispatch endPrepare : endPrepare(N)

//REQUISITO: command sent by the smart-phone of Maitre: clear
Dispatch clear : clear(N) 
Dispatch endClear : endClear(N)

//REQUISITO: command sent by the smart-phone of Maitre: add food command, by specifying a food-code
Dispatch addFood : addFood(FOODCODE)
Dispatch foodAdded : foodAdded(N)

Dispatch warning : warning(FOODCODE) //from addFood when codefood doesn't exist

/* --------------------------- */
// CONTESTI
/* --------------------------- */ 

Context ctxSprint5 ip [host="localhost" port=9001] +mqtt
Context ctxDummyScanningRoom ip [host="localhost" port=8068] +mqtt
Context ctxDummyNavigator ip [host="localhost" port=8038] +mqtt
Context ctxDummyPrepare ip [host="localhost" port=9002] +mqtt
Context ctxDummyClear ip [host="localhost" port=9003] +mqtt
Context ctxDummyParty ip [host="localhost" port=9005] +mqtt

/* --------------------------- */
// ATTORI
/* --------------------------- */

ExternalQActor roomperimeterexplorer context ctxDummyScanningRoom 
ExternalQActor findtable context ctxDummyScanningRoom
ExternalQActor navi context ctxDummyNavigator
ExternalQActor prepare context ctxDummyPrepare
ExternalQActor clear context ctxDummyClear
ExternalQActor addfood context ctxDummyParty

//attore dello sprint
QActor master5 context ctxSprint5 { 
	
	State s0 initial {
		println("MASTER 5 AVVIATO")
		run utils.backupLastLog()
		run utils.resetFile("fileLog")
		run utils.writeLog("actorName | [backToCompensate - ] currentPosition | move | result\\n")
		run itunibo.coap.fridge.createClient("localhost", 5684, "serverfridge") //CoAP access
	}
	Goto scanningperimeter
	
	//inizio la scansione del perimetro della stanza
	State scanningperimeter {
		println("")
		println("State scanningperimeter")
		println("Inizio scansione della stanza.")
		forward roomperimeterexplorer -m scanningPerimeter : scanningPerimeter	
	}
	Transition t0 whenMsg endPerimeter -> findtable
	
	//inizio ricerca del tavolo
	State findtable {
		println("")
		println("Fine scansione della stanza.")
		println("State findtable")
		println("Inizio ricerca del tavolo.")
		forward findtable -m scanningTable : scanningTable
	}
   	Transition t1 whenMsg homeReached -> executePrepare
        
    State executePrepare {
    	println("")
    	println("Fine ricerca del tavolo.")
		println("State executePrepare")
		delay 500
		println("Inizio prepare.")
		run itunibo.coap.fridge.put("expose") 
		delay 1000
        forward prepare -m prepare : prepare  
	}
	Transition t2 whenMsg endPrepare -> partyOnProgress
	
	State partyOnProgress {
		println("")
		println("Fine prepare")
		println("State partyOnProgress")
		delay 500
		//println("addFood di c003") //codice esistente
		//forward addfood -m addFood : addFood(c003)
		println("addFood di c040") //codice inesistente
		forward addfood -m addFood : addFood(c040)
	} 
	Transition t3 	whenMsg warning -> handleWarning
					whenMsg foodAdded -> executeClear
	
	State handleWarning {
		println("")
		println("State handleWarning")
		onMsg(warning : warning(FOODCODE)) {
			println("************** WARNING, il codice ${payloadArg(0).toString()} non esiste")	
		}
	}
	//dovrebbe ritornare in partyonprogress, ma per come è fatto ora, facciamo la clear e bona
	Goto executeClear
	
	State executeClear{
		println("Fine party")
		run itunibo.coap.fridge.put("expose")
		delay 1000
		println("")
		println("State executeClear")
		//run itunibo.coap.fridge.put("C000") //CHIEDERE AL PROF: va bene solo il codice o ci vuole la parola answer?
		println("Inizio clear.")
        forward clear -m clear : clear
	}
	Transition t4 whenMsg endClear -> endParty
	
	State endParty {
		println("Fine clear")
		println("")
		println("State next")
		delay 500
		run itunibo.coap.fridge.put("C000") 
		run itunibo.coap.fridge.put("C006") 
		run itunibo.coap.fridge.put("C040") //no
		run itunibo.coap.fridge.put("expose") 
		delay 1000
		println("Finito sprint5.")
	}
	
	
}