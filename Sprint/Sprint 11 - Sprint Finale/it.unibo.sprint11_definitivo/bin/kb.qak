System kb 
mqttBroker "localhost" : 1883

/* --------------------------- */  
// EVENTI
/* --------------------------- */

//REQUISITO: the Maitre is able, at any time to use his/her smart-phone to consult the state of the room 
Event consult : consult(N) //da frontend

/* --------------------------- */
// DISPATCH 
/* --------------------------- */
  
//DR = domestic resource
//FCL = food code list
//NAMECODE = stringa formata da nomecibo_codicecibo
//NAMECODEQTA = stringa formata da nomecibo_codicecibo_quantità
//CONTENT = contenuto da esporre
//FOODCODE = codice di un cibo
Dispatch moveObjToDomesticResource : moveObjToDomesticResource(DR) 		//da prepare e da clear
Dispatch moveFoodToDomesticResource : moveFoodToDomesticResource(DR) 	//da prepare, da clear e da addfood 
Dispatch moveObjOnRobot : moveObjOnRobot(DR) 							//da prepare e da clear
Dispatch moveFoodOnRobot : moveFoodOnRobot(DR, NAMECODE) 				//da fridge e da clear
Dispatch moveFoodToFridge : moveFoodToFridge(NAMECODEQTA) 				//a fridge
Dispatch ate : ate(FOODCODE)											//da greedy
Dispatch log : log(N)													//da rbr

/* --------------------------- */
// CONTESTI
/* --------------------------- */

Context ctxKb ip [host="localhost" port=9003] +mqtt
Context ctxDummyForFridge ip [host="localhost" port=9006] +mqtt
Context ctxDummyForParty ip [host="localhost" port=9007] +mqtt

/* --------------------------- */
// ATTORI
/* --------------------------- */

ExternalQActor prepare context ctxDummyForParty
ExternalQActor clear context ctxDummyForParty
ExternalQActor fridge context ctxDummyForFridge

//Attore per gestire la knowledge base
QActor kb context ctxKb {
	["var DomesticResource = \"\""]
	
	State s0 initial{
		println("KB AVVIATA")
		solve(consult("roomState.pl"))
	}
	Goto waitCmd

	State waitCmd{
		println("in attesa di un comando")
    }
	Transition t0 	whenEvent consult -> doExpose 						//da frontend
					whenMsg moveObjToDomesticResource -> objFromRobot	//da prepare e da clear
				  	whenMsg moveFoodToDomesticResource -> foodFromRobot	//da prepare, da clear e da addfood 
				  	whenMsg moveObjOnRobot -> objToRobot				//da prepare e da clear
				  	whenMsg moveFoodOnRobot -> foodToRobot				//da fridge e da clear
				  	whenMsg ate -> updateFoodOnTable					//da greedy
				  	whenMsg log -> writeLog
	
	//Trasferimento oggetti dal robot sul tavolo o nella dispensa o nella lavastoviglie
	State objFromRobot{
		println("")
		println("State objFromRobot")
		onMsg(moveObjToDomesticResource : moveObjToDomesticResource(DR)){
			["DomesticResource = payloadArg(0).toString()"]
		}
		solve(exposeObjectOnRobot(L))
		ifSolved{
			println("exposeObjectOnRobot = ${getCurSol(\"L\").toString()}")
			["var objOnRobot = getCurSol(\"L\").toString().replace(\"[\", \"\").replace(\"]\", \"\")"]
			println("Oggetti presenti sul robot: ${objOnRobot}")
			["for(obj in objOnRobot.split(\",\")){"]
				println("inserisco sul ${DomesticResource}: ${obj}")
				["when(DomesticResource) {
			  		\"table\" -> {"]
			  			//trasferimento oggetti dal robot al tavolo
			  			solve(assert(itemontable("${obj}")))
			  			run utils.writeLogKb("+.OT.${obj}")
						solve(retract(itemonrobot("${obj}")))
						run utils.writeLogKb("-.OR.${obj}")
			    	["}
					\"pantry\" -> {"]
						//trasferimento oggetti dal robot alla dispensa
			    		solve(assert(iteminpantry("${obj}")))
			    		run utils.writeLogKb("+.PA.${obj}")
						solve(retract(itemonrobot("${obj}")))
						run utils.writeLogKb("-.OR.${obj}")
				    ["}
					\"dishwasher\" -> {"]
						//trasferimento oggetti dal robot alla lavastoviglie
				    	solve(assert(itemindishwasher("${obj}")))
				    	run utils.writeLogKb("+.DW.${obj}")
						solve(retract(itemonrobot("${obj}")))
						run utils.writeLogKb("-.OR.${obj}")
					["}
				}
			}"]
		}
	}
	Goto doExpose
	
	//Trasferimento cibi dal robot sul tavolo o nel frigo
	State foodFromRobot{
		println("")  
		println("State foodFromRobot")
		onMsg(moveFoodToDomesticResource : moveFoodToDomesticResource(DR)){
			["DomesticResource = payloadArg(0).toString()"]
		}
		solve(exposeFoodOnRobot(L))
		ifSolved{
			println("exposeFoodOnRobot = ${getCurSol(\"L\").toString()}")
			["var foodOnRobot = getCurSol(\"L\").toString().replace(\"[\", \"\").replace(\"]\", \"\").replace(\"/\", \"\").replace(\")\", \"\").replace(\"'\", \"\")"]
			println("foodOnRobot = ${foodOnRobot}")
			["for(food in foodOnRobot.split(\"((\")){
				if(food != \"\"){
					var f = food.split(\",\")
					var name = f.get(0).toString()
					var code = f.get(1).toString()
					var qtyRobot = Integer.parseInt(f.get(2).toString())"] //qtà sul robot
					
					println("inserisco sul ${DomesticResource}: ${name} ${code} ${qtyRobot}")
					["when(DomesticResource) {
				  		\"table\" -> {"]
				  			//trasferimento cibo dal robot al tavolo
				  			solve(itemontable(NAME,"${code}",QTA))
				  			ifSolved{ //se c'è già il codice in tavola, aggiungi quantità
				  				["var qtyTable = Integer.parseInt(getCurSol(\"QTA\").toString())"]
				  				solve(retract(itemontable(NAME,"${code}",QTA)))
				  				run utils.writeLogKb("-.FT.${name},${code},${qtyTable}")
				  				
				  				["var qtySum = qtyRobot+qtyTable"]
				  				//solve(assert(itemontable("${name}","${code}","${qtySum}")))
				  				["solve(\"assert(itemontable('${name}','${code}',${qtySum}))\",\"\")"] //CHIEDERE AL PROF!!!!
				  				run utils.writeLogKb("+.FT.${name},${code},${qtySum}")
				  			}
				  			else {
					  			//solve(assert(itemontable("${name}","${code}","${qtyRobot}")))	
					  			["solve(\"assert(itemontable('${name}','${code}',${qtyRobot}))\",\"\")"] //CHIEDERE AL PROF!!!!
					  			run utils.writeLogKb("+.FT.${name},${code},${qtyRobot}")
							}
							//svuoto robot
							solve(retract(itemonrobot("${name}","${code}",QTA)))
							run utils.writeLogKb("-.FR.${name},${code},${qtyRobot}")
				    	["}
						\"fridge\" -> {"]
							[" val ToSend = \"${name}_${code}_${qtyRobot}\" "]
							forward fridge -m moveFoodToFridge : moveFoodToFridge($ToSend)
							//svuoto robot
							solve(retract(itemonrobot("${name}","${code}",QTA)))
							run utils.writeLogKb("-.FR.${name},${code},${qtyRobot}")
					    ["}
					}
				}
			}"]
		}
	}
	Goto doExpose
	
	//Trasferimento oggetti dal tavolo o dalla dispensa o dalla lavastoviglie sul robot
	State objToRobot{
		println("")
		println("State objToRobot")
		onMsg(moveObjOnRobot : moveObjOnRobot(DR)){
			["DomesticResource = payloadArg(0).toString()"]
		}
		["when(DomesticResource) {
			\"table\" -> {"]
				solve(exposeObjectOnTable(L))
				ifSolved{
					println("exposeObjectOnTable = ${getCurSol(\"L\").toString()}")
					["var objOnTable = getCurSol(\"L\").toString().replace(\"[\", \"\").replace(\"]\", \"\")"]
					println("Oggetti presenti sul tavolo: ${objOnTable}")
					["for(obj in objOnTable.split(\",\")){"]
						println("inserisco sul robot: ${obj}")
						solve(assert(itemonrobot("${obj}")))
						run utils.writeLogKb("+.OR.${obj}")
						
						solve(retract(itemontable("${obj}")))
						run utils.writeLogKb("-.OT.${obj}")
					["}"]
				}
			["}
			\"pantry\" -> {"]
				solve(exposePantry(L))
				ifSolved{
					println("exposePantry = ${getCurSol(\"L\").toString()}")
					["var objInPantry = getCurSol(\"L\").toString().replace(\"[\", \"\").replace(\"]\", \"\")"]
					println("Oggetti presenti nella dispensa: ${objInPantry}")
					["for(obj in objInPantry.split(\",\")){"]
						println("inserisco sul robot: ${obj}")
						solve(assert(itemonrobot("${obj}")))
						run utils.writeLogKb("+.OR.${obj}")
						
						solve(retract(iteminpantry("${obj}")))
						run utils.writeLogKb("-.PA.${obj}")
					["}"]
				}
			["}
			\"dishwasher\" -> {"]
				//per sviluppi futuri: qui non entra mai. Finita la lavastoviglie, le stoviglie tornano alla dispensa
				solve(exposeDishwasher(L))
				ifSolved{
					println("exposeDishwasher = ${getCurSol(\"L\").toString()}")
					["var objInDishwasher = getCurSol(\"L\").toString().replace(\"[\", \"\").replace(\"]\", \"\")"]
					println("Oggetti presenti nella lavastoviglie: ${objInDishwasher}")
					["for(obj in objInDishwasher.split(\",\")){"]
						println("inserisco sul robot: ${obj}")
						solve(assert(itemonrobot("${obj}")))
						run utils.writeLogKb("+.OR.${obj}")
						
						solve(retract(itemindishwasher("${obj}")))
						run utils.writeLogKb("-.DW.${obj}")
					["}"]
				}
			["}
		}"]	
	}
	Goto doExpose
	
	//Trasferimento cibi dal tavolo e dal frigo sul robot
	State foodToRobot{
		println("")
		println("State foodToRobot")
		["var name = \"\"
		var code = \"\""] 
		onMsg(moveFoodOnRobot : moveFoodOnRobot(DR, NAMECODE)){ //da fridge e da clear
			["DomesticResource = payloadArg(0).toString()
			name = payloadArg(1).toString().split(\"_\").get(0).toString()				
			code = payloadArg(1).toString().split(\"_\").get(1).toString()"]
			println("${name}, ${code}")
		}
		["when(DomesticResource) {
			\"table\" -> {"] //table non guarda nameFood e codeFood, abbiamo stringa "all_all"
			//NOTA: eventualmente si potrebbe cambiare il messaggio o gestire con due signature diverse
				solve(exposeFoodOnTable(L))
				ifSolved{
					println("exposeFoodOnTable = ${getCurSol(\"L\").toString()}")
					["var foodOnTable = getCurSol(\"L\").toString().replace(\"[\", \"\").replace(\"]\", \"\").replace(\"/\", \"\").replace(\")\", \"\").replace(\"'\", \"\")"]
					println("Cibo presente sul tavolo: ${foodOnTable}") 
					["for(food in foodOnTable.split(\"((\")){
						if(food != \"\"){
							var f = food.split(\",\")
							var nameFood = f.get(0).toString()
							var codeFood = f.get(1).toString()
							var qtyFood = Integer.parseInt(f.get(2).toString())"]
							println("inserisco sul robot: ${nameFood} ${codeFood} ${qtyFood}")
							solve(assert(itemonrobot("${nameFood}","${codeFood}","${qtyFood}")))
							run utils.writeLogKb("+.FR.${nameFood},${codeFood},${qtyFood}")
							
							solve(retract(itemontable("${nameFood}","${codeFood}",QTA)))
							run utils.writeLogKb("-.FT.${nameFood},${codeFood},${qtyFood}")
						["}
					}"]
				}
			["}
			\"fridge\" -> {"]
				//il robot ha preso una qtà del cibo dal frigo. Assert sulla sua kb
				solve(itemonrobot("${name}","${code}",QTA))
		    	ifSolved{
		    		println("risolta ricerca codice-cibo")
		    		["var qty = Integer.parseInt(getCurSol(\"QTA\").toString())+1"]
					solve(retract(itemonrobot("${name}","${code}",QTA)))
					run utils.writeLogKb("-.FR.${name},${code},${qty}")
					
					["solve(\"assert(itemonrobot('${name}','${code}',${qty}))\",\"\")"] //CHIEDERE AL PROF!!!!!!!!!!!!
		    		//solve(assert(itemonrobot("${name}","${code}","${qty}")))
		    		run utils.writeLogKb("+.FR.${name},${code},${qty}")
		    	}
		    	else{
		    		solve(assert(itemonrobot("${name}","${code}",1))) //1 perché quantità è fissa
		    		run utils.writeLogKb("+.FR.${name},${code},1")
		    	}
			["}
		}"]	
	}
	Goto doExpose
    
    //Consumo dei cibi
    State updateFoodOnTable{
     	println("")
		println("State objFromRobot")
		["var FoodCode = \"\""]
		onMsg(ate : ate(FOODCODE)){
			["FoodCode = payloadArg(0).toString()"]
			solve(itemontable(NAME,"${FoodCode}",QTA))
			ifSolved{
				["var qtyTable = Integer.parseInt(getCurSol(\"QTA\").toString())
 				var name = getCurSol(\"NAME\").toString()"]
  				solve(retract(itemontable(NAME,"${FoodCode}",QTA)))
  				run utils.writeLogKb("-.FT.${name},${FoodCode},${qtyTable}")
  				
  				["var qty = qtyTable-1"]
  				if "qty != 0" {
	  				//solve(assert(itemontable("${name}","${code}","${qty}")))
	  				["solve(\"assert(itemontable('${name}','${FoodCode}',${qty}))\",\"\")"] //CHIEDERE AL PROF!!!!
	  				run utils.writeLogKb("+.FT.${name},${FoodCode},${qty}")
  				}
			}
		}
    }
    Goto doExpose
    
	//stato che espone lo stato della stanza
	State doExpose{
		println("") 
		println("State doExpose")
		//non c'è il frigo perché è gestito da fridgecoap
		solve(expose(OT, FT, P, D, OR, FR))
		ifSolved{     
			["var ObjInPatry = utils.prepareToSend(\"pantry\", getCurSol(\"P\").toString(), \"objects\")
			var ObjInDishwasher = utils.prepareToSend(\"dishwasher\", getCurSol(\"D\").toString(), \"objects\")
			var ObjFoodOnTable = utils.prepareToSend(\"table\", getCurSol(\"OT\").toString() + \"%\" + getCurSol(\"FT\").toString(), \"objects and food\")
			var ObjFoodOnRobot = utils.prepareToSend(\"robot\", getCurSol(\"OR\").toString() + \"%\" + getCurSol(\"FR\").toString(), \"objects and food\")"]
      
			println("Objects in pantry: ${ObjInPatry}")
			println("Objects in dishwasher: ${ObjInDishwasher}")
			println("Objects on table: ${ObjFoodOnTable}")
			println("Objects on robot: ${ObjFoodOnRobot}")
			
			//Emit per mqttUtils.js
			run itunibo.frontend.frontendSupport.updateFrontend(myself, ObjInPatry)
			run itunibo.frontend.frontendSupport.updateFrontend(myself, ObjInDishwasher)
			run itunibo.frontend.frontendSupport.updateFrontend(myself, ObjFoodOnTable)
			run itunibo.frontend.frontendSupport.updateFrontend(myself, ObjFoodOnRobot)
		} 
	}
	Goto waitCmd
	
	State writeLog{
		println("") 
		println("State writeLog")
		//non c'è il frigo perché è gestito da fridgecoap
		solve(expose(OT, FT, P, D, OR, FR))
		ifSolved{			
			run utils.writeLogKb("CONSULT")
			run utils.writeLogKb("PA: ${getCurSol(\"P\").toString().replace(\"/\", \"\").replace(\")\", \"\").replace(\"'\", \"\").replace(\"(\", \" \")}")
			run utils.writeLogKb("FT: ${getCurSol(\"FT\").toString().replace(\"/\", \"\").replace(\")\", \"\").replace(\"'\", \"\").replace(\"(\", \" \")}")
			run utils.writeLogKb("OT: ${getCurSol(\"OT\").toString().replace(\"/\", \"\").replace(\")\", \"\").replace(\"'\", \"\").replace(\"(\", \" \")}")
			run utils.writeLogKb("DW: ${getCurSol(\"D\").toString().replace(\"/\", \"\").replace(\")\", \"\").replace(\"'\", \"\").replace(\"(\", \" \")}")
			run utils.writeLogKb("FR: ${getCurSol(\"FR\").toString().replace(\"/\", \"\").replace(\")\", \"\").replace(\"'\", \"\").replace(\"(\", \" \")}")
			run utils.writeLogKb("OR: ${getCurSol(\"OR\").toString().replace(\"/\", \"\").replace(\")\", \"\").replace(\"'\", \"\").replace(\"(\", \" \")}")
		}
	}
	Goto waitCmd
} 